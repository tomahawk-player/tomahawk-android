buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.14.0'
    }
}

import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "com.android.support:appcompat-v7:21.0.0"
    compile "com.android.support:support-v4:21.0.0"
    compile "se.emilsjolander:stickylistheaders:2.4.1"
    compile("ch.acra:acra:4.5.0") {
        transitive = false
    }
    compile "com.fasterxml.jackson.core:jackson-databind:2.4.1.3"
    compile "com.google.guava:guava:18.0"
    compile "com.squareup.picasso:picasso:2.3.3"
    compile "com.github.castorflex.verticalviewpager:library:19.0.0"
    compile "com.github.castorflex.smoothprogressbar:library:1.0.0"
    compile "de.mrmaffen:vlc-android-sdk:1.0.3"
    compile "de.mrmaffen:rdio-android-sdk:1.0.0"
    compile "de.mrmaffen:deezer-android-sdk:1.0.0"
    compile "de.mrmaffen:holocircularprogressbar:1.0.1"
    compile files("libs/RemoteMetadataProvider.jar")
    compile "org.apache.lucene:lucene-core:4.7.2"
    compile "org.apache.lucene:lucene-analyzers-common:4.7.2"
    compile "org.apache.lucene:lucene-queryparser:4.7.2"
    compile "net.sourceforge.findbugs:jsr305:1.3.7"
    compile "com.squareup.retrofit:retrofit:1.6.1"
    compile 'com.sothree.slidinguppanel:library:2.0.2'
    compile "com.nineoldandroids:library:2.4.0"
    compile('com.uservoice:uservoice-android-sdk:1.1.2') {
        transitive = false
    }
}

/*
 * Gets the version name from the latest Git tag
 */
def getNewVersionName = { ->
    boolean isChanged = 'git diff --cached'.execute().text.length() > 0 ||
            'git diff'.execute().text.length() > 0
    return 'git describe --tags'.execute().text.replace('\n', '') + (isChanged ? "_modified" : "")
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.0"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 21
        renderscriptTargetApi 21
        renderscriptSupportModeEnabled true
        versionName '0.75'
        versionCode 750
        ndk {
            moduleName "libspotifywrapper"
            stl "stlport_static"
            ldLibs "log", "OpenSLES"
            cFlags " -std=c++0x -D__STDC_INT64__"
        }
    }

    lintOptions {
        abortOnError false
    }


    dexOptions {
        jumboMode = true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/ASL2.0'
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDir 'libs'
            jni.srcDirs = [] //disable automatic ndk-build call
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = outputFile.name.replace(".apk", "-" + getNewVersionName() + ".apk")
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    buildTypes {
        release {
            zipAlignEnabled true
            minifyEnabled true
            proguardFile 'proguard-android.txt'
        }
        debug {
            versionNameSuffix "_debug"
            zipAlignEnabled true
        }
    }

    productFlavors {
        arm {
            versionName getNewVersionName() + "_arm"
            versionCode Integer.parseInt("1" + defaultConfig.versionCode)
            ndk {
                abiFilters "armeabi", "armeabi-v7a"
            }
        }
    }
}

////////////////////////////////////////////////////
// NDK Support (by https://gist.github.com/pboos) //
////////////////////////////////////////////////////
// Make sure the environment variable $ANDROID_NDK_HOME is set:
// example: ANDROID_NDK_HOME /home/maffen/SDKs/android-ndk-r8e
task ndkBuild(type: Exec) {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine new File((String) System.env.ANDROID_NDK_HOME, 'ndk-build.cmd')
    } else {
        commandLine new File((String) System.env.ANDROID_NDK_HOME, 'ndk-build')
    }
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn ndkBuild
}

/*
 * Include the file "signingconfig.gradle" (which defines the signingConfig), if it exists
 * Example of the contents of such "signingconfig.gradle":
 *
 * android {
 *    signingConfigs {
 *       release {
 *           storeFile file("key.keystore")
 *           storePassword "password"
 *           keyAlias "alias"
 *           keyPassword "password"
 *       }
 *    }
 *
 *    buildTypes {
 *       release {
 *           signingConfig signingConfigs.release
 *       }
 *    }
 * }
 *
 */
if (new File("signingconfig.gradle").exists()) {
    apply from: "signingconfig.gradle";
}